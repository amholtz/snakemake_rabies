import os

# To run locally:
# snakemake --snakefile snakefile_phylogeography --keep-going --cores 4 --config folder=.. --use-singularity --singularity-prefix ~/.singularity --singularity-args "--home ~"

# To visualise the pipeline
# snakemake --snakefile snakefile_phylogeography --config folder=.. --dag | dot -Tsvg > pipeline_phylogeography.svg

localrules: all, pastml_itol, fig
#ruleorder: pastml_col > nex2nwk

folder = os.path.abspath(config["folder"])
data_dir = os.path.join(folder, 'data')

os.makedirs('logs', exist_ok=True)

rule all:
    input:
        #os.path.join(data_dir, 'dates_lsd.tab'),
        #os.path.join(data_dir, 'iq_tree.ntips'),
        #os.path.join(data_dir, 'raxmlng_tree.ntips'),
        #os.path.join(data_dir, 'iq_tree.root.lsd2.nexus'),
        #os.path.join(data_dir, 'raxmlgn_tree.root.lsd2.nexus'),
        #os.path.join(data_dir, 'iTOL_colorstrip-species.txt'),
        #os.path.join(data_dir, 'bats_split.txt'),
        #os.path.join(data_dir, 'skunks_split.txt'),
        #os.path.join(data_dir, 'dogs_split.txt'),
        os.path.join(data_dir, 'arctic_foxes_split.txt'),


rule extract_dates_lsd:
    '''
    Reformats metadata into date file readable by LSD.
    '''
    input:
        tab = os.path.join(data_dir, 'ncbi_cleaned.tab')
    output:
        tab = os.path.join(data_dir, 'dates_lsd.tab')
    params:
        mem = 1000,
        name = 'id',
        date_col = 'collection_date',
        qos = 'normal',
    threads: 1
    singularity: "docker://evolbioinfo/python-evol:v3.6"
    shell:
        """
        python3 py/extract_dates_lsd.py --data {input.tab} --dates {output.tab} \
        --date_col {params.date_col}
        """

rule tree_size:
    '''
    Calculates tree size.
    '''
    input:
        tree_iq = os.path.join(data_dir, 'iq_tree.nwk'),
        tree_rax = os.path.join(data_dir, 'raxmlng_tree.nwk'),

    output:
        length_iq = os.path.join(data_dir, 'iq_tree.ntips'),
        length_rax = os.path.join(data_dir, 'raxmlng_tree.ntips'),
    singularity:
        "docker://evolbioinfo/gotree:v0.3.0b"
    params:
        mem = 2000,
        name = 'tree_size',
        qos = 'fast'
    threads: 1
    shell:
        """
        gotree stats -i {input.tree_iq} | tail -n 1 | awk '{{print $3}}' > {output.length_iq}
        gotree stats -i {input.tree_rax} | tail -n 1 | awk '{{print $3}}' > {output.length_rax}
        """

rule iq_date_lsd2:
    '''
    Dates a tree and roots the tree according to dates
    no -v
    '''
    input:
        aln_length =  os.path.join(data_dir, 'aln.length'),
        tree = os.path.join(data_dir, 'iq_tree.nwk'),
        ntips = os.path.join(data_dir, 'iq_tree.ntips'),
        dates = os.path.join(data_dir, 'dates_lsd.tab'),
    output:
        tree = os.path.join(data_dir, 'iq_tree.root.lsd2.nexus'),
        log = os.path.join(data_dir, 'iq_tree.root.lsd2.log'),
        rd = os.path.join(data_dir, 'iq_tree.root.lsd2.rootdate'),
        outliers = os.path.join(data_dir, 'iq_tree.outliers'),
    threads: 1
    singularity: "docker://evolbioinfo/lsd2:v1.6.5"
    params:
        mem = 4000,
        name = 'lsd2',
        wd = os.path.join(data_dir, 'iq_tree.root.wd_lsd2'),
        qos = 'normal',
    shell:
        """
        n=`head {input.aln_length}`
        t=`head {input.ntips}`
        m=`awk -v t="$t" 'BEGIN {{ m=int(t * 0.33); print m}}'`

        echo "lsd2 -i {input.tree} -d {input.dates} -s $n -o {params.wd} -f 1000 -e 3 -r a"

        lsd2 -i {input.tree} -d {input.dates} -s $n -o {params.wd} -f 1000 -e 3 -r a

        pattern_g='\\- The input dates associated with the following'
        pattern_s='\\- The input dates associated with the following [0-9]* nodes are considered as outliers, so the nodes were removed from the analysis:  '

        outliers=`grep -i "$pattern_g" {params.wd} | sed -e "s/$pattern_s//g"`
        for o in $outliers
        do
            echo $o >> {output.outliers}
        done

        mv "{params.wd}.date.nexus" "{output.tree}"
        mv "{params.wd}" "{output.log}"
        rm -rf {params.wd}*

        echo `grep "], tMRCA" {output.log} | sed -e 's/rate [0-9\\.e+-]\\+ \\[[0-9\\.e+-]\\+; [0-9\\.e+-]\\+\\], tMRCA //g' \
        | sed -e 's/ \\[[0-9\\.e+-]\\+; [0-9\\.e+-]\\+\\], objective function [0-9\\.e+-]\\+//g'` > {output.rd}
        """

        
rule raxmlng_date_lsd2:
    '''
    Dates a tree and roots the tree according to dates
    no -v
    '''
    input:
        aln_length =  os.path.join(data_dir, 'aln.length'),
        tree = os.path.join(data_dir, 'raxmlng_tree.nwk'),
        ntips = os.path.join(data_dir, 'raxmlng_tree.ntips'),
        dates = os.path.join(data_dir, 'dates_lsd.tab'),
    output:
        tree = os.path.join(data_dir, 'raxmlgn_tree.root.lsd2.nexus'),
        log = os.path.join(data_dir, 'raxmlgn_tree.root.lsd2.log'),
        rd = os.path.join(data_dir, 'raxmlgn_tree.root.lsd2.rootdate'),
        outliers = os.path.join(data_dir, 'raxmlgn_tree.outliers'),
    threads: 1
    singularity: "docker://evolbioinfo/lsd2:v1.6.5"
    params:
        mem = 4000,
        name = 'lsd2',
        wd = os.path.join(data_dir, 'raxmlgn_tree.root.wd_lsd2'),
        qos = 'normal',
    shell:
        """
        n=`head {input.aln_length}`
        t=`head {input.ntips}`
        m=`awk -v t="$t" 'BEGIN {{ m=int(t * 0.33); print m}}'`

        echo "lsd2 -i {input.tree} -d {input.dates} -s $n -o {params.wd} -f 1000 -e 3 -r a"

        lsd2 -i {input.tree} -d {input.dates} -s $n -o {params.wd} -f 1000 -e 3 -r a

        pattern_g='\\- The input dates associated with the following'
        pattern_s='\\- The input dates associated with the following [0-9]* nodes are considered as outliers, so the nodes were removed from the analysis:  '

        outliers=`grep -i "$pattern_g" {params.wd} | sed -e "s/$pattern_s//g"`
        for o in $outliers
        do
            echo $o >> {output.outliers}
        done

        mv "{params.wd}.date.nexus" "{output.tree}"
        mv "{params.wd}" "{output.log}"
        rm -rf {params.wd}*

        echo `grep "], tMRCA" {output.log} | sed -e 's/rate [0-9\\.e+-]\\+ \\[[0-9\\.e+-]\\+; [0-9\\.e+-]\\+\\], tMRCA //g' \
        | sed -e 's/ \\[[0-9\\.e+-]\\+; [0-9\\.e+-]\\+\\], objective function [0-9\\.e+-]\\+//g'` > {output.rd}
        """
rule itol_annotations:
    '''
    Converts a tab-separated table into iTOL annotations.
    '''
    input:
        species = os.path.join(data_dir, 'ncbi_cleaned.tab'),
    output:
        species = os.path.join(data_dir, 'iTOL_colorstrip-species.txt'),
    singularity:
        "docker://evolbioinfo/table2itol"
    params:
        mem = 2000,
        name = 'table2itol',
        qos = 'fast',
        path = data_dir
    threads: 1
    shell:
        """
        mkdir -p {params.path}
        table2itol.R -i id -D {params.path} {input.species}
        """

rule split_global_tree:
    '''
    Splits the global rabies tree construction into three separate groups by bat, skunk, and dog clade
    '''
    input:
        tree = os.path.join(data_dir, 'iq_tree_dated_rooted.nwk'),
    output:
        bats = os.path.join(data_dir, 'bats_split.txt'),
        skunks = os.path.join(data_dir, 'skunks_split.txt'),
        dogs = os.path.join(data_dir, 'dogs_split.txt'),
        arctic_foxes = os.path.join(data_dir, 'arctic_foxes_split.txt'),
    singularity:
        "docker://evolbioinfo/python-evol:v3.6richer"
    params:
        mem = 500,
        name = 'split',
        qos = 'fast'
    threads: 1
    shell:
        """
        python3 py/split_tree_parts.py --input_tree {input.tree} --output_bats {output.bats} \
        --output_skunks {output.skunks} --output_dogs {output.dogs} --output_arctic_foxes {output.arctic_foxes}
        """